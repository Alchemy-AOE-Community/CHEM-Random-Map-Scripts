/* ****************   Penguin Nothing   **********

   Author: Zetnus   zetnus@outlook.com
   created:    16 December 2020
   Most recent update: 05 May 2022


based off of my Antarctica map


Learn about random map scripting: https://docs.google.com/document/d/e/2PACX-1vR_I1I9u-hI2FFm-EYyjoM_-9dNJFOfTaIgr05wXNbdpv9tI18b6r18ARULy3Jqyvlq6-lc2VjX6xP4/pub

This map was harvested from a mod in the official AOE2DE mod center, whose licensing agreement with Microsoft supersedes any Github restrictions: https://www.microsoft.com/en-us/servicesagreement

*/

/* constants */
#const ICE_SHALLOWS 26
#const UNDERBRUSH_LEAVES 71
#const UNDERBRUSH_SNOW 72
#const SNOW_LIGHT 73
#const SNOW_STRONG 74

#const ICE_PATCH 728
#const SPAWN_PENGUIN 1029
#const SNOW_BUSH 1054
#const LUMBER 1320
#const REED_TREE 1350

/* placeholder objects */
#const ONGRID_PLACEHOLDER_1 278
#const BLANKOBJECT 647
#const ON_GRID 1291
#const ONGRID_PLACEHOLDER_2 1613
#const BLOCKER 1776

/* =================================================================================================== */
<PLAYER_SETUP>

random_placement 
ai_info_map_type CUSTOM 0 0 0
behavior_version 1

effect_amount SET_ATTRIBUTE REED_TREE ATTR_NAME_ID 5323
effect_amount SET_ATTRIBUTE FORAGE ATTR_NAME_ID 5324
effect_amount SET_ATTRIBUTE REED_TREE ATTR_DEAD_ID 1350

effect_amount GAIA_SET_ATTRIBUTE DLC_PENGUIN ATTR_DEAD_ID 833
effect_amount GAIA_SET_ATTRIBUTE TURKEY ATTR_HITPOINTS 0
effect_amount SET_ATTRIBUTE TURKEY ATTR_NAME_ID 5734
effect_amount SET_ATTRIBUTE TURKEY ATTR_ICON_ID 157

/* prevent relics on ice */
effect_amount GAIA_SET_ATTRIBUTE RELIC ATTR_TERRAIN_ID 8

/* Disabling Trade Carts and Cogs for Multi-team Games */
if 2_TEAM_GAME
else
  #const TRADE_CART_ENABLER 161
  #const TRADE_COG_ENABLER 180
  #const TRADE_CARAVAN_ENABLER 48
  #const HINDU_CARAVANSERAI_ENABLER 518
  #const PERSIAN_CARAVANSERAI_ENABLER 552  
  #const SILK_ROAD 499
  effect_amount DISABLE_TECH TRADE_COG_ENABLER ATTR_DISABLE 180
  effect_amount DISABLE_TECH TRADE_CART_ENABLER ATTR_DISABLE 161
  effect_amount DISABLE_TECH TRADE_CARAVAN_ENABLER ATTR_DISABLE 48
  effect_amount DISABLE_TECH HINDU_CARAVANSERAI_ENABLER ATTR_DISABLE 518
  effect_amount DISABLE_TECH PERSIAN_CARAVANSERAI_ENABLER ATTR_DISABLE 552
  effect_amount DISABLE_TECH SILK_ROAD ATTR_DISABLE 499    
endif

/* =================================================================================================== */
<LAND_GENERATION>
base_terrain WATER

create_player_lands {
	terrain_type ICE_SHALLOWS
	base_size 12
	land_percent 78
	left_border 16
	right_border 16
	top_border 16
	bottom_border 16
	border_fuzziness 1
	zone 1  
}

/* =================================================================================================== */
<ELEVATION_GENERATION>

create_elevation 3 {
	base_terrain ICE_SHALLOWS
	if TINY_MAP	number_of_clumps 10
	elseif SMALL_MAP	number_of_clumps 15
	elseif MEDIUM_MAP	number_of_clumps 20
	elseif LARGE_MAP	number_of_clumps 27
	elseif HUGE_MAP		number_of_clumps 33
	elseif GIGANTIC_MAP	number_of_clumps 40
	elseif LUDIKRIS_MAP	number_of_clumps 160
	endif
	number_of_tiles 1500
	set_scale_by_size
	enable_balanced_elevation
}

/* =================================================================================================== */
<TERRAIN_GENERATION>

color_correction CC_WINTER

create_terrain DLC_NEWSHALLOW {
	base_terrain ICE_SHALLOWS
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
}
create_terrain SNOW_LIGHT {
	base_layer DLC_NEWSHALLOW
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	spacing_to_other_terrain_types 2
	terrain_mask 1
}
create_terrain SNOW_STRONG {
	base_layer SNOW_LIGHT 
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain SNOW {
	base_terrain ICE_SHALLOWS
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	spacing_to_other_terrain_types 5
}
create_terrain SNOW_STRONG {
	base_terrain SNOW
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	set_avoid_player_start_areas
}

/* forests will be on this terrain */
create_terrain SNOW_LIGHT {
	base_terrain SNOW_STRONG
	land_percent 8
	number_of_clumps 40
	set_scale_by_groups
	spacing_to_other_terrain_types 3
	set_avoid_player_start_areas
}

/* cosmetic terrain mixing */
create_terrain ICYSHORE {
	base_terrain SNOW_STRONG
	land_percent 6
	number_of_clumps 16
	set_scale_by_groups
	terrain_mask 1
	spacing_to_other_terrain_types
}
create_terrain UNDERBRUSH_SNOW {
	base_terrain SNOW_STRONG
	land_percent 8
	number_of_clumps 16
	set_scale_by_groups
	terrain_mask 1
}
create_terrain GRAVEL_WET {
	base_terrain SNOW_STRONG
	land_percent 8
	number_of_clumps 16
	set_scale_by_groups
	terrain_mask 1
}
create_terrain DLC_NEWSHALLOW {
	base_terrain SNOW_STRONG
	land_percent 8
	number_of_clumps 16
	set_scale_by_groups
	terrain_mask 1
}

create_terrain DLC_QUICKSAND {
	base_terrain SNOW
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
	spacing_to_other_terrain_types 5
}

/* water mixing */
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 100
	number_of_clumps 9320
	terrain_mask 1
	spacing_to_other_terrain_types 1
}
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 2
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 2
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 2
}
create_terrain SNOW {
	base_terrain ICE_SHALLOWS
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	height_limits 1 8
}

create_terrain DEEP_WATER {
	base_terrain ICE_SHALLOWS
	land_percent 100
	number_of_clumps 9320
	set_scale_by_groups
	terrain_mask 1
}


/* =================================================================================================== */
<OBJECTS_GENERATION>


/* make the "forests" */
#const TERRAIN_BLOCKER 1776
create_object REED_TREE {
	number_of_objects 2
	group_placement_radius 1
	number_of_groups 9320
	set_scaling_to_map_size
	terrain_to_place_on SNOW_LIGHT
	temp_min_distance_group_placement 18
	resource_delta 99950
	second_object BLOCKER
	actor_area 999
	actor_area_radius 0
}

create_object ON_GRID {
	number_of_objects 9320
	set_scaling_to_map_size
    set_place_for_every_player
    set_gaia_object_only
    actor_area_to_place_in 999
    second_object LUMBER
	actor_area 1400
	actor_area_radius 6
}



if INFINITE_RESOURCES #define DEATH_MATCH
endif

/* Town Center generation */
create_object TOWN_CENTER
{
	set_place_for_every_player
	max_distance_to_players 0
}

if SUDDEN_DEATH
	create_object FLAG_C {
		set_place_for_every_player
		number_of_objects 4
		min_distance_to_players 3
		find_closest
	}
endif

create_object FORAGE
{
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_group_placement 1
	resource_delta 4875
	min_distance_to_players 3
}
create_object GOLD
{
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_group_placement 1
	resource_delta 200
	min_distance_to_players 3
}
create_object SNOW_BUSH
{
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_group_placement 1
	resource_delta 9900
	second_object HAY_STACK
	min_distance_to_players 3
}

/* starting villagers */
create_object VILLAGER {
	set_place_for_every_player
	temp_min_distance_group_placement 1
	find_closest
}

if REGICIDE	create_object KING {
	set_place_for_every_player
	find_closest
}
endif

if EMPIRE_WARS
/* please contact Zetnus to implement empire wars */
endif

if INFINITE_RESOURCES
else
create_object BLANKOBJECT
{
	number_of_objects rnd(220,320)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 9
	actor_area 6009
	actor_area_radius 0
}
create_object BLANKOBJECT 
{
	number_of_objects rnd(240,320)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 11
	actor_area 6011
	actor_area_radius 0
}
create_object BLANKOBJECT 
{
	number_of_objects rnd(290,440)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 13
	actor_area 6013
	actor_area_radius 0
}
create_object BLANKOBJECT
{
	number_of_objects rnd(290,440)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 15
	actor_area 6015
	actor_area_radius 0
}
create_object BLANKOBJECT
{
	number_of_objects rnd(400,500)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 17
	actor_area 6017
	actor_area_radius 0
}
create_object BLANKOBJECT 
{
	number_of_objects rnd(500,600)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 20
	actor_area 6020
	actor_area_radius 0
}

create_object BLANKOBJECT 
{
	number_of_objects rnd(600,700)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 23
	actor_area 6023
	actor_area_radius 0
}
endif

create_object RELIC
{
	if TINY_MAP
		number_of_objects 5
		temp_min_distance_group_placement 30
	elseif SMALL_MAP
		number_of_objects 5
		temp_min_distance_group_placement 33
	elseif MEDIUM_MAP
		number_of_objects 5
		temp_min_distance_group_placement 33
	elseif LARGE_MAP
		number_of_objects 7
		temp_min_distance_group_placement 43
	elseif HUGE_MAP
		number_of_objects 8
		temp_min_distance_group_placement 47
	else
		number_of_objects 999
		temp_min_distance_group_placement 52 
	endif
	min_distance_to_players 25
	set_circular_placement
	avoid_forest_zone 2
	avoid_cliff_zone 3
	min_distance_group_placement 1
}



/* Primary Gold Mine */
create_object GOLD
{
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	avoid_actor_area 6009
	min_distance_to_players 10
	find_closest
	max_distance_to_other_zones 7
	avoid_cliff_zone 3
	actor_area 12
	avoid_forest_zone 2
	actor_area 12
	actor_area_radius 5
	avoid_actor_area 14
	avoid_actor_area 1400
	min_distance_group_placement 2
}
create_object BLANKOBJECT 
{
	number_of_objects 9
	set_gaia_object_only
	set_place_for_every_player
	actor_area_to_place_in 12	/* primary gold */
	actor_area 1212
	actor_area_radius 12
}

create_object GOLD
{
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 18
	find_closest
	avoid_actor_area 6017
	max_distance_to_other_zones 7
	avoid_forest_zone 1
	avoid_cliff_zone 3
	actor_area 61
	actor_area_radius 5
		avoid_actor_area 14 /* berries */
		avoid_actor_area 1400
		avoid_actor_area 12 /* Primary gold, only use if no ew */
	avoid_actor_area 1212 /* blank object on the primary gold - further distance than actor area 12 */
	min_distance_group_placement 3
}

create_object GOLD
{
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 21
	find_closest
	avoid_actor_area 6020
	min_distance_group_placement 3
	max_distance_to_other_zones 7
	avoid_forest_zone 1
	avoid_cliff_zone 3
	actor_area 62
	actor_area_radius 5
		avoid_actor_area 14 /* berries */
		avoid_actor_area 1400
		avoid_actor_area 12 /* Primary gold, only use if no ew */
	avoid_actor_area 61	/* medium gold */
	avoid_actor_area 1212	/* blank object on the primary gold - further distance than actor area 12 */
	min_distance_group_placement 3
}

create_object STONE
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	find_closest
	avoid_actor_area 6011
	min_distance_group_placement 3
	max_distance_to_other_zones 7
	avoid_forest_zone 1
	avoid_cliff_zone 3
	actor_area 63
	actor_area_radius 12
		avoid_actor_area 14 /* berries */
		avoid_actor_area 1400
		avoid_actor_area 12 /* Primary gold, only use if no ew */
	avoid_actor_area 61	/* medium gold */
	avoid_actor_area 62	/* far gold */
}

create_object STONE
{
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 16
	find_closest
	avoid_actor_area 6015
	min_distance_group_placement 3
	max_distance_to_other_zones 7
	avoid_forest_zone 1
	avoid_cliff_zone 3
	actor_area 65
	actor_area_radius 5
		avoid_actor_area 14 /* berries */
		avoid_actor_area 1400
		avoid_actor_area 12 /* Primary gold, only use if no ew */
		avoid_actor_area 61 /* medium gold */
		avoid_actor_area 62 /* far gold */
		avoid_actor_area 63 /* near stone */
}

/* Animals */
if DEATH_MATCH
else
create_object DLC_PIG
{
	number_of_objects 4
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 7
	set_circular_placement
	find_closest
	avoid_forest_zone 2
	avoid_cliff_zone 3
} 

create_object DLC_GOOSE
{
   number_of_objects 2
   number_of_groups 8
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 14
   max_distance_to_players 30
   set_circular_placement
   temp_min_distance_group_placement 8
}

endif

create_object DLC_STORK
{
	number_of_objects 6
	set_scaling_to_map_size
}

/* scattered gold and stone  */

create_object GOLD
{
	if TINY_MAP
		number_of_objects 3
		number_of_groups 2
		temp_min_distance_group_placement 30
	elseif SMALL_MAP
		number_of_objects 3
		number_of_groups 2	
		temp_min_distance_group_placement 30
	elseif MEDIUM_MAP
		number_of_objects 3
		number_of_groups 3
		temp_min_distance_group_placement 30
	elseif LARGE_MAP
		number_of_objects 3
		number_of_groups 3
		temp_min_distance_group_placement 30
	elseif HUGE_MAP
		number_of_objects 4
		number_of_groups 4
		temp_min_distance_group_placement 30
	elseif GIGANTIC_MAP
		number_of_objects 4
		number_of_groups 5	
		temp_min_distance_group_placement 30
	elseif LUDIKRIS_MAP
		 number_of_objects 4
		 number_of_groups 26
		 temp_min_distance_group_placement 44
	endif
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players 40
	set_circular_placement
	avoid_forest_zone 1
	avoid_cliff_zone 4
	actor_area 66
	actor_area_radius 5
	min_distance_group_placement 2
}

create_object STONE
{
	if TINY_MAP
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 30
	elseif SMALL_MAP
		number_of_objects 2
		number_of_groups 2
		temp_min_distance_group_placement 30
	elseif MEDIUM_MAP
		number_of_objects 3
		number_of_groups 3
		temp_min_distance_group_placement 30
	elseif LARGE_MAP
		number_of_objects 4
		number_of_groups 3
		temp_min_distance_group_placement 30
	elseif HUGE_MAP
		number_of_objects 4
		number_of_groups 4
		temp_min_distance_group_placement 30
	elseif GIGANTIC_MAP
		number_of_objects 4
		number_of_groups 9320
		temp_min_distance_group_placement 30
	elseif LUDIKRIS_MAP
		number_of_objects 4
		number_of_groups 9320
		temp_min_distance_group_placement 44
	endif
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players 40
	set_circular_placement
	avoid_forest_zone 1 
	avoid_cliff_zone 4
	actor_area 67
	actor_area_radius 5
}

create_object SHIPWRECK
{
	set_scaling_to_player_number
	set_gaia_object_only
	terrain_to_place_on ICYSHORE
	temp_min_distance_group_placement  30
}
create_object SHIPWRECK2
{
	set_scaling_to_player_number
	set_gaia_object_only
	terrain_to_place_on ICYSHORE
	temp_min_distance_group_placement  40
}

/* penguin party time! */
create_object DLC_PENGUIN {
	number_of_objects 9320
	set_scaling_to_map_size
	set_gaia_object_only
	set_gaia_unconvertible
	min_distance_to_players 12
	temp_min_distance_group_placement 2
}


/* fish */
create_object SHORE_FISH
{
	number_of_objects  9320
	set_scaling_to_map_size
	set_gaia_object_only
	terrain_to_place_on WATER
	min_distance_group_placement 3
	temp_min_distance_group_placement  4
}
create_object SALMON
{
	number_of_objects  9320
	set_scaling_to_map_size
	set_gaia_object_only
	terrain_to_place_on MED_WATER
	min_distance_group_placement 4
	temp_min_distance_group_placement  6
}
create_object TUNA
{
	number_of_objects  9320
	set_scaling_to_map_size
	set_gaia_object_only
	terrain_to_place_on DEEP_WATER
	min_distance_group_placement 6
	temp_min_distance_group_placement  8
}
create_object MARLIN1
{
	number_of_objects  9320
	set_scaling_to_map_size
	set_gaia_object_only
	terrain_to_place_on DLC_WATER4
	min_distance_group_placement 8
	temp_min_distance_group_placement  12
}
create_object MARLIN2
{
	number_of_objects  9320
	set_scaling_to_map_size
	set_gaia_object_only
	terrain_to_place_on DLC_WATER4
	min_distance_group_placement 8
	temp_min_distance_group_placement  12
}












