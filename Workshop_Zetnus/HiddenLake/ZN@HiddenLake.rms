/* Hidden Lake */
/*

Most recent update: 05 May 2022

By: Zetnus 
Zetnus@outlook.com

-created for Spring Brawl
-only works in DE

Learn about random map scripting: https://docs.google.com/document/d/e/2PACX-1vR_I1I9u-hI2FFm-EYyjoM_-9dNJFOfTaIgr05wXNbdpv9tI18b6r18ARULy3Jqyvlq6-lc2VjX6xP4/pub

This map was harvested from a mod in the official AOE2DE mod center, whose licensing agreement with Microsoft supersedes any Github restrictions: https://www.microsoft.com/en-us/servicesagreement

*/

/* =================================================================================================== */
<PLAYER_SETUP>
random_placement
ai_info_map_type CUSTOM 0 0 0
behavior_version 1

/* Disabling Trade Carts and Cogs for Multi-team Games */
if 2_TEAM_GAME
else
  #const TRADE_CART_ENABLER 161
  #const TRADE_COG_ENABLER 180
  #const TRADE_CARAVAN_ENABLER 48
  #const HINDU_CARAVANSERAI_ENABLER 518
  #const PERSIAN_CARAVANSERAI_ENABLER 552  
  #const SILK_ROAD 499
  effect_amount DISABLE_TECH TRADE_COG_ENABLER ATTR_DISABLE 180
  effect_amount DISABLE_TECH TRADE_CART_ENABLER ATTR_DISABLE 161
  effect_amount DISABLE_TECH TRADE_CARAVAN_ENABLER ATTR_DISABLE 48
  effect_amount DISABLE_TECH HINDU_CARAVANSERAI_ENABLER ATTR_DISABLE 518
  effect_amount DISABLE_TECH PERSIAN_CARAVANSERAI_ENABLER ATTR_DISABLE 552
  effect_amount DISABLE_TECH SILK_ROAD ATTR_DISABLE 499    
endif

/* =================================================================================================== */
<LAND_GENERATION>

base_terrain GRASS

/* center */
create_land {
	terrain_type PALM_DESERT
	land_percent 100
	base_size 1
	top_border 38
	right_border 38
	left_border 38
	bottom_border 38
	border_fuzziness 100
	land_position 50 50
	zone 2
	land_id 2
	other_zone_avoidance_distance 2
}

create_player_lands {
	terrain_type GRASS
	land_percent 100
	top_border 5
	right_border 5
	left_border 5
	bottom_border 5
	border_fuzziness 100
	circle_radius 38 0
	base_elevation 3
	zone 1
	other_zone_avoidance_distance 2
}


/* =================================================================================================== */
<ELEVATION_GENERATION>
/* we need this section, even though it is empty, so that the base elevation generates properly */


/* =================================================================================================== */
<TERRAIN_GENERATION>

/* set up the required terrains */
create_terrain GRASS3 {
	base_terrain GRASS
	land_percent 100
	number_of_clumps 999
	set_avoid_player_start_areas
	terrain_mask 2
}
create_terrain DLC_DRYGRASS {
	base_terrain GRASS3
	land_percent 100
	number_of_clumps 999
	spacing_to_other_terrain_types 3
	terrain_mask 2
}
create_terrain DLC_DESERTGRAVEL {
	base_terrain DLC_DRYGRASS
	land_percent 100
	number_of_clumps 999
	height_limits 0 2
	terrain_mask 2
}
create_terrain DLC_DESERTGRAVEL {
	base_terrain GRASS3
	land_percent 100
	number_of_clumps 999
	height_limits 0 2
}
create_terrain DLC_ROCK {
	base_terrain DLC_DESERTGRAVEL
	land_percent 100
	number_of_clumps 999
	height_limits 1 2
}

/* forest clumps - might need to be tweaked */
create_terrain MEDITERRANEAN_FOREST {
	base_terrain DLC_DRYGRASS
	land_percent 12
	number_of_clumps 16
	set_scale_by_groups
	spacing_to_other_terrain_types 1
}

/* cosmetic terrain mixing */
create_terrain DIRT2 {
	base_terrain DLC_DRYGRASS
	land_percent 8
	number_of_clumps 8
	set_scale_by_groups
	terrain_mask 1
}

/* make the lake */
create_terrain WATER {
	base_terrain PALM_DESERT
	land_percent 100
	number_of_clumps 999
	spacing_to_other_terrain_types 7
}
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 100
	terrain_mask 1
	spacing_to_other_terrain_types 1
}
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}


/* =================================================================================================== */
<CONNECTION_GENERATION>

/* used to create the springs and streams */
create_connect_to_nonplayer_land {
	replace_terrain GRASS3 WATER
	terrain_cost GRASS3 5
	terrain_size GRASS3 3 0
	
	replace_terrain DLC_DRYGRASS SHALLOW
	terrain_cost DLC_DRYGRASS 5
	terrain_size DLC_DRYGRASS 1 0
	
	replace_terrain MEDITERRANEAN_FOREST SHALLOW
	terrain_cost MEDITERRANEAN_FOREST 5
	terrain_size MEDITERRANEAN_FOREST 1 0
	
	replace_terrain PALM_DESERT DLC_NEWSHALLOW
	terrain_cost PALM_DESERT 5
	terrain_size PALM_DESERT 1 0
	
	replace_terrain BEACH DLC_NEWSHALLOW
	terrain_cost BEACH 5
	terrain_size BEACH 1 0
	
	replace_terrain DLC_DESERTGRAVEL MED_WATER
	terrain_cost DLC_DESERTGRAVEL 5
	terrain_size DLC_DESERTGRAVEL 1 0
	
	replace_terrain DLC_ROCK MED_WATER
	terrain_cost DLC_ROCK 5
	terrain_size DLC_ROCK 1 0
}

/* =================================================================================================== */
<OBJECTS_GENERATION>

/* block the beaches in the center */
#const REED_TREE 1350
create_object REED_TREE {
	number_of_objects 9320
	set_scaling_to_map_size
	terrain_to_place_on BEACH
	place_on_forest_zone
	set_gaia_object_only
}

/* starting objects */
create_object TOWN_CENTER {
	set_place_for_every_player
	max_distance_to_players 0
}

create_object VILLAGER {
	set_place_for_every_player
	min_distance_to_players 5
	max_distance_to_players 7
	set_circular_placement
	min_distance_group_placement 1
}

create_object SCOUT {
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 6
		set_circular_placement
		find_closest
		min_distance_group_placement 1
	}
endif

/* close fish */
create_object PLANT {
	set_place_for_every_player
	terrain_to_place_on WATER
	find_closest
	set_gaia_object_only
	second_object SALMON
	actor_area 1
	actor_area_radius 12
}
/* far fish */
create_object PLANT {
	number_of_objects 2
	set_place_for_every_player
	terrain_to_place_on MED_WATER
	find_closest
	set_gaia_object_only
	second_object SALMON
	temp_min_distance_group_placement 2
}

if REGICIDE
	create_object VILLAGER {
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 7
		set_circular_placement
	}
	create_object CASTLE {
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 10
		set_circular_placement
		min_distance_group_placement 2
		avoid_actor_area 1
	}
endif

/* DE relic balance */
create_object RELIC {
	max_distance_to_other_zones 2
	avoid_forest_zone 2
	min_distance_to_players 25
	set_circular_placement
	min_distance_group_placement 1
	if TINY_MAP
	   number_of_objects            5
	   temp_min_distance_group_placement 35
	elseif SMALL_MAP
	   number_of_objects            5
	   temp_min_distance_group_placement 38
	elseif MEDIUM_MAP
	   number_of_objects            5
	   temp_min_distance_group_placement 38
	elseif LARGE_MAP
	   number_of_objects            7
	   temp_min_distance_group_placement 48
	elseif HUGE_MAP
	   number_of_objects            8
	   temp_min_distance_group_placement 52
	else
	   number_of_objects            999
	   temp_min_distance_group_placement 52 
	endif
}

/* forage - one less than usual because of the fish */
create_object DLC_ORANGEBUSH {
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 9
	max_distance_to_players 13
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 2
	avoid_actor_area 1
}

/* straggler trees */
create_object DLC_DRAGONTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	set_circular_placement
	min_distance_group_placement 1
}
create_object DLC_DRAGONTREE {
	number_of_objects 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

/* near gold */
create_object GOLD {
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 16
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 3
}

/* medium gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 50
			min_distance_to_players 17
			max_distance_to_players 22
		percent_chance 50
			min_distance_to_players 20
			max_distance_to_players 25
	end_random
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* far gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 50
			min_distance_to_players 23
			max_distance_to_players 8
		percent_chance 50
			min_distance_to_players 27
			max_distance_to_players 32
	end_random
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* near stone */
create_object STONE {
	number_of_objects 5
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 18
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* far stone */
create_object STONE {
	number_of_objects 4
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 50
			min_distance_to_players 20
			max_distance_to_players 24
		percent_chance 50
			min_distance_to_players 22
			max_distance_to_players 27
	end_random
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* sheep - one less because of the the fish */
create_object DLC_PIG {
	number_of_objects 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 8
	set_circular_placement
	avoid_forest_zone 2
}
create_object SHEEP {
	number_of_objects 2
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 20
			min_distance_to_players 14
			max_distance_to_players 17
		percent_chance 20
			min_distance_to_players 18
			max_distance_to_players 21
		percent_chance 20
			min_distance_to_players 20
			max_distance_to_players 23
		percent_chance 20
			min_distance_to_players 24
			max_distance_to_players 27
		percent_chance 20
			min_distance_to_players 27
			max_distance_to_players 30
	end_random
	set_circular_placement
	avoid_forest_zone 2
}

create_object GAZELLE {
	start_random
		percent_chance 50	number_of_objects 4
		percent_chance 50	number_of_objects 3
	end_random
	number_of_groups 1
	group_placement_radius 3
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	start_random
		percent_chance 20
			min_distance_to_players 14
			max_distance_to_players	17
		percent_chance 20
			min_distance_to_players 18
			max_distance_to_players 21
		percent_chance 20
			min_distance_to_players 21
			max_distance_to_players 24
		percent_chance 20
			min_distance_to_players 24
			max_distance_to_players 27
		percent_chance 20
			min_distance_to_players 27
			max_distance_to_players 30
	end_random
	set_circular_placement
}

create_object BOAR {
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	min_distance_to_players 16	/* further distances removed to make boar stealing harder and reduce unlucky map generations */
	max_distance_to_players 19
	set_circular_placement
}

/* neutral gold */
create_object GOLD {
	terrain_to_place_on DLC_DRYGRASS
	if TINY_MAP
		number_of_groups 2
		number_of_objects 3
		temp_min_distance_group_placement 52
	elseif SMALL_MAP
		number_of_groups 3
		number_of_objects 3
		temp_min_distance_group_placement 56
	elseif MEDIUM_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 56
	elseif LARGE_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 56
	else
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 64
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	min_distance_to_players 36
	set_circular_placement
	avoid_forest_zone 3
}

/* neutral stone */
create_object STONE {
	terrain_to_place_on DLC_DRYGRASS
	if TINY_MAP		number_of_objects 2
	elseif SMALL_MAP	number_of_objects 2
	elseif MEDIUM_MAP
		number_of_groups 9999
		number_of_objects 3
		temp_min_distance_group_placement 56
	elseif LARGE_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 56
	elseif HUGE_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 60
	else
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 64
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	min_distance_to_players 36
	set_circular_placement
	avoid_forest_zone 3
}

create_object WOLF {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 33
			min_distance_to_players 30
			max_distance_to_players 33
		percent_chance 33
			min_distance_to_players 34
			max_distance_to_players 37
		percent_chance 34
			min_distance_to_players 37
			max_distance_to_players 40
	end_random
	set_circular_placement
	temp_min_distance_group_placement 15
	avoid_forest_zone 2
}

/* scattered wolves */
create_object WOLF {
	number_of_groups 9999
	set_loose_grouping
	start_random
		percent_chance 75	number_of_objects 1
		percent_chance 25	number_of_objects 2
	end_random
	set_gaia_object_only
	min_distance_group_placement 1
	temp_min_distance_group_placement 60
	avoid_forest_zone 2
	min_distance_to_players 40
	set_circular_placement
} 

/* scattered stragglers */
create_object DLC_DRAGONTREE {
	terrain_to_place_on DLC_DRYGRASS
	number_of_objects 20
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players 8
	min_distance_group_placement 1
	temp_min_distance_group_placement 12
	avoid_forest_zone 1
}

/* fish */
create_object MARLIN1 {
	number_of_objects 9999
	place_on_specific_land_id 2
	if LUDIKRIS_MAP max_distance_to_players 40
	else			max_distance_to_players 10
	endif
	set_gaia_object_only
	max_distance_to_other_zones 4
	min_distance_group_placement 3
	temp_min_distance_group_placement 5
}

create_object SALMON {
	number_of_objects 9999
	place_on_specific_land_id 2
	if LUDIKRIS_MAP max_distance_to_players 43
	else			max_distance_to_players 13
	endif
	set_gaia_object_only
	temp_min_distance_group_placement 3
}

/* birds */
create_object DLC_STORK {
	number_of_objects 6
	set_scaling_to_map_size
}
Â´



