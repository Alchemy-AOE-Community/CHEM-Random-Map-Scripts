/* Deadwood Forest

Created: 20 July 2013
Last edit: 05 May 2022
   
by: Zetnus
Zetnus@outlook.com
Zetnus#6414 on Discord
   
A cross between black forest and sherwood forest where all the trees are dead.

Enjoy!
   
Learn about random map scripting: https://docs.google.com/document/d/e/2PACX-1vR_I1I9u-hI2FFm-EYyjoM_-9dNJFOfTaIgr05wXNbdpv9tI18b6r18ARULy3Jqyvlq6-lc2VjX6xP4/pub

This map was harvested from a mod in the official AOE2DE mod center, whose licensing agreement with Microsoft supersedes any Github restrictions: https://www.microsoft.com/en-us/servicesagreement

*/

#const UNDERBRRUSH_LEAVES 71
#const ROAD_MOSS 39
#const CLIFF_TERRAIN 16


/* =================================================================================================== */
<PLAYER_SETUP>
random_placement
ai_info_map_type BLACK_FOREST 0 0 0
behavior_version 1

/* Disabling Trade Carts and Cogs for Multi-team Games */
if 2_TEAM_GAME
else
  #const TRADE_CART_ENABLER 161
  #const TRADE_COG_ENABLER 180
  #const TRADE_CARAVAN_ENABLER 48
  #const HINDU_CARAVANSERAI_ENABLER 518
  #const PERSIAN_CARAVANSERAI_ENABLER 552  
  #const SILK_ROAD 499
  effect_amount DISABLE_TECH TRADE_COG_ENABLER ATTR_DISABLE 180
  effect_amount DISABLE_TECH TRADE_CART_ENABLER ATTR_DISABLE 161
  effect_amount DISABLE_TECH TRADE_CARAVAN_ENABLER ATTR_DISABLE 48
  effect_amount DISABLE_TECH HINDU_CARAVANSERAI_ENABLER ATTR_DISABLE 518
  effect_amount DISABLE_TECH PERSIAN_CARAVANSERAI_ENABLER ATTR_DISABLE 552
  effect_amount DISABLE_TECH SILK_ROAD ATTR_DISABLE 499    
endif

/* =================================================================================================== */
<LAND_GENERATION>

base_terrain LEAVES

create_player_lands {
	terrain_type DIRT2
	land_percent 29
	other_zone_avoidance_distance 10
	base_size 12
}


/* =================================================================================================== */
<ELEVATION_GENERATION>

create_elevation 3 {
	base_terrain LEAVES
	number_of_clumps 16
	number_of_tiles 2000
	set_scale_by_size
	enable_balanced_elevation
}

create_elevation 5 {
	base_terrain LEAVES
	number_of_clumps 10
	number_of_tiles 2000
	set_scale_by_size
	enable_balanced_elevation
}

create_elevation 7 {
	base_terrain LEAVES
	number_of_clumps 8
	number_of_tiles 2000
	set_scale_by_size
	enable_balanced_elevation
}

/* =================================================================================================== */
<CLIFF_GENERATION>

cliff_type CT_DESERT
min_distance_cliffs  5 
if TINY_MAP
	min_number_of_cliffs 5
	max_number_of_cliffs 11
	min_length_of_cliff 4
	max_length_of_cliff 6
elseif SMALL_MAP
	min_number_of_cliffs 8
	max_number_of_cliffs 17
	min_length_of_cliff 4
	max_length_of_cliff 7
elseif MEDIUM_MAP
	min_number_of_cliffs 11
	max_number_of_cliffs 22
	min_length_of_cliff 5
	max_length_of_cliff 8
elseif LARGE_MAP
	min_number_of_cliffs 16
	max_number_of_cliffs 32
	min_length_of_cliff 6
	max_length_of_cliff 9
elseif HUGE_MAP
	min_number_of_cliffs 19
	max_number_of_cliffs 38
	min_length_of_cliff 6
	max_length_of_cliff 10
elseif GIGANTIC_MAP
	min_number_of_cliffs 23
	max_number_of_cliffs 46
	min_length_of_cliff 6
	max_length_of_cliff 10
elseif LUDIKRIS_MAP
	min_number_of_cliffs 92
	max_number_of_cliffs 184
	min_length_of_cliff 6
	max_length_of_cliff 10
endif


/* =================================================================================================== */
<TERRAIN_GENERATION>

create_terrain UNDERBRRUSH_LEAVES {
	base_terrain DIRT2
	number_of_clumps 14
	land_percent 7
	clumping_factor 6
	terrain_mask 1
}
create_terrain DIRT3 {
	base_terrain DIRT2
	number_of_clumps 14
	land_percent 7
	clumping_factor 6
	terrain_mask 1
}
create_terrain LEAVES {
	base_terrain DIRT2
	number_of_clumps 14
	land_percent 7
	clumping_factor 6
	terrain_mask 1
}


create_terrain UNDERBRRUSH_LEAVES {
	base_terrain LEAVES
	number_of_clumps 30
	land_percent 3
	clumping_factor 6
}
create_terrain DIRT3 {
	base_terrain LEAVES
	number_of_clumps 30
	land_percent 4
	terrain_mask 2
}
create_terrain DIRT2 {
	base_terrain LEAVES
	number_of_clumps 30
	land_percent 8
	terrain_mask 2
}
create_terrain DIRT2 {
	base_terrain LEAVES
	number_of_clumps 30
	land_percent 6
	terrain_mask 2
}

create_terrain DLC_DRYGRASS {
	base_terrain CLIFF_TERRAIN
	number_of_clumps 9320
	land_percent 100
}

/* =================================================================================================== */
<CONNECTION_GENERATION>

create_connect_all_players_land {
	replace_terrain LEAVES DIRT2
	terrain_cost LEAVES 7
	terrain_cost DLC_DRYGRASS 0
	terrain_size LEAVES 2 0
}

create_connect_teams_lands {
	default_terrain_replacement ROAD_MOSS
	terrain_cost LEAVES 13
	terrain_cost DLC_DRYGRASS 0
	
	terrain_cost DIRT2 10
	terrain_cost UNDERBRRUSH_LEAVES 10
	terrain_cost DIRT3 10
	
	terrain_size ROAD_MOSS 0 1
}


/* =================================================================================================== */
<OBJECTS_GENERATION>

create_object DLC_DEADTREE {
	number_of_groups 50
	number_of_objects 10
	group_variance 4
	set_tight_grouping
	min_distance_group_placement 0
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}
create_object DLC_DEADTREE {
	number_of_groups 50
	number_of_objects 10
	group_variance 4
	set_tight_grouping
	min_distance_group_placement 0
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}
create_object TREE_G {
	number_of_groups 50
	number_of_objects 10
	group_variance 4
	set_tight_grouping
	min_distance_group_placement 0
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}

/*  player stuff */
create_object TOWN_CENTER {
	set_place_for_every_player
	max_distance_to_players 0
}

create_object VILLAGER {
	set_place_for_every_player
	min_distance_to_players 5
	max_distance_to_players 7
	set_circular_placement
	min_distance_group_placement 1
}

create_object SCOUT {
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 6
		set_circular_placement
		find_closest
		min_distance_group_placement 1
	}
	create_object VILLAGER {
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		set_circular_placement
	}
	create_object CASTLE {
		set_place_for_every_player
		min_distance_to_players 9
		max_distance_to_players 11
		set_circular_placement
		min_distance_group_placement 2
	}
endif


create_object FORAGE {
	number_of_objects 5
	number_of_groups 1
	min_distance_to_players 8
	max_distance_to_players 10
	set_circular_placement
	set_gaia_object_only
	set_place_for_every_player
	set_tight_grouping
	min_distance_group_placement 4
} 

/* near gold */
create_object GOLD {
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 16
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 3
}

/* medium gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 17
	max_distance_to_players 23
	set_circular_placement
	min_distance_group_placement 4
}

/* far gold */
create_object GOLD {
	number_of_groups 3
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 25
	max_distance_to_players 35
	set_circular_placement
	min_distance_group_placement 4
	temp_min_distance_group_placement 14
}

/* near stone */
create_object STONE {
	number_of_objects 5
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 14
	max_distance_to_players 18
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 6
	avoid_forest_zone 2
}

/* far stone */
create_object STONE {
	number_of_groups 3
	number_of_objects 4
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 18
	max_distance_to_players 28
	set_circular_placement
	min_distance_group_placement 4
	temp_min_distance_group_placement 16
}

create_object DLC_GOAT {
	number_of_groups 1
	number_of_objects 4
	min_distance_to_players 7
	max_distance_to_players 9
	set_circular_placement
	set_gaia_object_only
	set_place_for_every_player
	min_distance_group_placement 4 
}

create_object DLC_GOAT {
	number_of_groups 2
	number_of_objects 2
	min_distance_to_players 17
	max_distance_to_players 22
	set_circular_placement
	set_gaia_object_only
	set_place_for_every_player
	min_distance_group_placement 4
}

create_object BOAR {
	number_of_objects 1
	number_of_groups 2
	set_loose_grouping  
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 16
	max_distance_to_players 22
	set_circular_placement
}

create_object DEER {
	number_of_groups 2
	number_of_objects 3
	min_distance_group_placement 
	set_place_for_every_player
	min_distance_to_players 18
	max_distance_to_players 22
	set_circular_placement
	set_gaia_object_only
	temp_min_distance_group_placement 10
}

create_object TREE_F {
	number_of_objects 3
	min_distance_to_players 3
	max_distance_to_players 9
	set_circular_placement
	set_place_for_every_player
	set_gaia_object_only
}
create_object TREE_I {
	number_of_objects 3
	min_distance_to_players 3
	max_distance_to_players 9
	set_circular_placement
	set_place_for_every_player
	set_gaia_object_only
}

/* =================================================================================================== */
/* scattered map resources */ 

create_object RELIC {
	max_distance_to_other_zones 2
	min_distance_to_players 27
	set_circular_placement
	min_distance_group_placement 1
	if TINY_MAP
		number_of_objects 5
		temp_min_distance_group_placement 35
	elseif SMALL_MAP
		number_of_objects 5
		temp_min_distance_group_placement 38
	elseif MEDIUM_MAP
		number_of_objects 5
		temp_min_distance_group_placement 38
	elseif LARGE_MAP
		number_of_objects 7
		temp_min_distance_group_placement 48
	elseif HUGE_MAP
		number_of_objects 8
		temp_min_distance_group_placement 52
	else
		number_of_objects 999
		temp_min_distance_group_placement 52 
	endif
}

/* extra gold */
create_object GOLD {
	if TINY_MAP
		number_of_groups 2
		number_of_objects 3
		temp_min_distance_group_placement 46
	elseif SMALL_MAP
		number_of_groups 3
		number_of_objects 4
		temp_min_distance_group_placement 52
	elseif MEDIUM_MAP
		number_of_groups 9320
		number_of_objects 4
		group_variance 1
		temp_min_distance_group_placement 56
	elseif LARGE_MAP
		number_of_groups 9320
		number_of_objects 4
		group_variance 1
		temp_min_distance_group_placement 56
	else
		number_of_groups 9320
		number_of_objects 4
		group_variance 1
		temp_min_distance_group_placement 64
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	min_distance_to_players 40
	set_circular_placement
}

/* extra stone */
create_object STONE {
	if TINY_MAP
		number_of_objects 2
		number_of_groups 3
		temp_min_distance_group_placement 52
	elseif SMALL_MAP
		number_of_objects 2
		number_of_groups 4
		temp_min_distance_group_placement 56
	elseif MEDIUM_MAP
		number_of_groups 9320
		number_of_objects 3
		temp_min_distance_group_placement 56
	elseif LARGE_MAP
		number_of_groups 9320
		number_of_objects 4
		group_variance 1
		temp_min_distance_group_placement 56
	elseif HUGE_MAP
		number_of_groups 9320
		number_of_objects 4
		group_variance 1
		temp_min_distance_group_placement 60
	else
		number_of_groups 9320
		number_of_objects 4
		group_variance 1
		temp_min_distance_group_placement 64
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	min_distance_to_players 40
	set_circular_placement
	avoid_forest_zone 3
}

/* extra berries */
create_object FORAGE {
    number_of_groups 9320
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	number_of_objects 4
	min_distance_to_players 40
	set_circular_placement
	temp_min_distance_group_placement 112
	avoid_forest_zone 3
}

create_object DEER {
	number_of_objects 4
	number_of_groups 8
	group_variance 2
	set_gaia_object_only
	min_distance_group_placement 2
	temp_min_distance_group_placement 10
	min_distance_to_players 40
	set_circular_placement
	set_scaling_to_map_size
}

create_object WOLF {
	number_of_objects 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_group_placement 2
	temp_min_distance_group_placement 10
	min_distance_to_players 30
	max_distance_to_players 40
	set_circular_placement
}

create_object WOLF {
	number_of_groups 8
	set_loose_grouping
	start_random
		percent_chance 75	number_of_objects 1
		percent_chance 25	number_of_objects 2
	end_random
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement 2
	temp_min_distance_group_placement 10
	min_distance_to_players 40
	set_circular_placement
} 

create_object DLC_DEADTREE {
	number_of_groups 40
	number_of_objects 7
	group_variance 2
	set_loose_grouping
	min_distance_group_placement 2
	set_gaia_object_only
	min_distance_to_players 12
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}

create_object DLC_DEADTREE {
	number_of_groups 40
	number_of_objects 7
	group_variance 2
	set_loose_grouping
	min_distance_group_placement 2
	set_gaia_object_only
	min_distance_to_players 12
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}

create_object TREE_G {
	number_of_groups 40
	number_of_objects 7
	group_variance 2
	set_loose_grouping
	min_distance_group_placement 2
	set_gaia_object_only
	min_distance_to_players 12
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}

create_object DLC_DEADTREE {
	number_of_groups 250
	number_of_objects 2
	set_loose_grouping
	min_distance_group_placement 1
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}

create_object DLC_DEADTREE {
	number_of_groups 250
	number_of_objects 2
	set_loose_grouping
	min_distance_group_placement 1
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}

create_object TREE_G {
	number_of_groups 250
	number_of_objects 2
	set_loose_grouping
	min_distance_group_placement 1
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	terrain_to_place_on LEAVES
	set_scaling_to_map_size
}


/* extra eye-candy */

create_object STUMP {
	number_of_groups 450
	number_of_objects 4
	group_variance 3
	set_loose_grouping
	min_distance_group_placement 0
	set_gaia_object_only
	min_distance_to_players 9
	set_circular_placement
	set_scaling_to_map_size
}

create_object SKELETON {
	number_of_objects 15
	set_scaling_to_map_size
}

create_object BROKEN_CART {
	number_of_objects 2
	set_scaling_to_map_size
	min_distance_to_players 18
	set_circular_placement
	temp_min_distance_group_placement 30
	min_distance_group_placement 2
}

create_object GRAVE {
	number_of_objects 4
	set_scaling_to_map_size
	min_distance_to_players 9
	set_circular_placement
}

create_object RUINS {
	number_of_objects 1
	set_scaling_to_map_size
	min_distance_to_players 40
	set_circular_placement
	temp_min_distance_group_placement 40
	min_distance_group_placement 3
}

create_object DLC_VULTURE {
	number_of_objects 5
	set_scaling_to_map_size
}


/* a couple more trees */
create_object TREE_I {
	number_of_groups 25
	number_of_objects 1
	set_loose_grouping
	temp_min_distance_group_placement 7
	set_gaia_object_only
	min_distance_to_players 25
	set_circular_placement
	set_scaling_to_map_size
	min_distance_group_placement 4
}
create_object TREE_F {
	number_of_groups 25
	number_of_objects 1
	set_loose_grouping
	temp_min_distance_group_placement 7
	set_gaia_object_only
	min_distance_to_players 25
	set_circular_placement
	set_scaling_to_map_size
	min_distance_group_placement 4
}
create_object TREE_G {
	number_of_groups 25
	number_of_objects 1
	set_loose_grouping
	temp_min_distance_group_placement 7
	set_gaia_object_only
	min_distance_to_players 25
	set_circular_placement
	set_scaling_to_map_size
	min_distance_group_placement 4
}
