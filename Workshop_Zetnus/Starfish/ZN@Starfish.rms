/* Starfish */

/*

Most recent update: 05 April 2022

By: Zetnus 
Zetnus@outlook.com

-created for Spring Brawl
-only works in DE

Learn about random map scripting: https://docs.google.com/document/d/e/2PACX-1vR_I1I9u-hI2FFm-EYyjoM_-9dNJFOfTaIgr05wXNbdpv9tI18b6r18ARULy3Jqyvlq6-lc2VjX6xP4/pub

This map was harvested from a mod in the official AOE2DE mod center, whose licensing agreement with Microsoft supersedes any Github restrictions: https://www.microsoft.com/en-us/servicesagreement

*/


#const GRASS_CLIFFS 16
#const BEACH_DRY 79
#const BEACH_WET 80
#const GRAVEL_WET 81
#const GRAVEL_DRY 70
#const GRAVEL_DESERT 102
#const GRASS_JUNGLE 83
#const UNDERBRUSH_JUNGLE 77
#const UNDERBRUSH_LEAVES 71



/* =================================================================================================== */
<PLAYER_SETUP>
random_placement
ai_info_map_type MEDITERRANEAN 0 0 0
behavior_version 1

/* Disabling Trade Carts and Cogs for Multi-team Games */
if 2_TEAM_GAME
else
  #const TRADE_CART_ENABLER 161
  #const TRADE_COG_ENABLER 180
  #const TRADE_CARAVAN_ENABLER 48
  #const HINDU_CARAVANSERAI_ENABLER 518
  #const PERSIAN_CARAVANSERAI_ENABLER 552  
  #const SILK_ROAD 499
  effect_amount DISABLE_TECH TRADE_COG_ENABLER ATTR_DISABLE 180
  effect_amount DISABLE_TECH TRADE_CART_ENABLER ATTR_DISABLE 161
  effect_amount DISABLE_TECH TRADE_CARAVAN_ENABLER ATTR_DISABLE 48
  effect_amount DISABLE_TECH HINDU_CARAVANSERAI_ENABLER ATTR_DISABLE 518
  effect_amount DISABLE_TECH PERSIAN_CARAVANSERAI_ENABLER ATTR_DISABLE 552
  effect_amount DISABLE_TECH SILK_ROAD ATTR_DISABLE 499    
endif

/* =================================================================================================== */
<LAND_GENERATION>

base_terrain GRASS

create_player_lands {
	terrain_type GRASS_CLIFFS
	land_percent 10
	base_size 8
	top_border 10
	right_border 10
	left_border 10
	bottom_border 10
	border_fuzziness 2
	circle_radius 41 0
	zone 1
}

/* center */
create_land {
	terrain_type GRAVEL_DESERT
	land_percent 100
	base_size 1
	top_border 22
	right_border 22
	left_border 22
	bottom_border 22
	border_fuzziness 100
	land_position 50 50
}

/* =================================================================================================== */
<TERRAIN_GENERATION>

color_correction CC_JUNGLE

create_terrain GRASS {
	base_terrain GRASS_CLIFFS
	land_percent 100
	number_of_clumps 999
	set_avoid_player_start_areas
	terrain_mask 2
}

create_terrain GRASS {
	base_terrain GRAVEL_DESERT
	land_percent 100
	number_of_clumps 999
	terrain_mask 1
}

create_terrain GRAVEL_DRY {
	base_terrain GRAVEL_DESERT
	land_percent 100
	number_of_clumps 999
	spacing_to_other_terrain_types 4
	terrain_mask 2
}
create_terrain GRAVEL_WET {
	base_terrain GRAVEL_DRY
	land_percent 100
	number_of_clumps 999
	spacing_to_other_terrain_types 4
	terrain_mask 2
}

create_terrain BEACH_WET {
	base_terrain GRAVEL_WET
	land_percent 100
	number_of_clumps 999
	spacing_to_other_terrain_types 4
	terrain_mask 2
}

create_terrain BEACH_DRY {
	base_terrain BEACH_WET
	land_percent 100
	number_of_clumps 999
	spacing_to_other_terrain_types 4
	terrain_mask 2
}


/* make the lake */

create_terrain DLC_NEWSHALLOW {
	base_terrain BEACH_DRY
	land_percent 100
	spacing_to_other_terrain_types 4
}
create_terrain DLC_WATER5 {
	base_terrain DLC_NEWSHALLOW
	land_percent 100
	terrain_mask 1
	spacing_to_other_terrain_types 1
}
create_terrain DLC_WATER5 {
	base_terrain DLC_NEWSHALLOW
	land_percent 100
	spacing_to_other_terrain_types 2
}
create_terrain WATER {
	base_terrain DLC_WATER5
	land_percent 100
	terrain_mask 1
	spacing_to_other_terrain_types 1
}
create_terrain WATER {
	base_terrain DLC_WATER5
	land_percent 100
	spacing_to_other_terrain_types 2
}
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 100
	terrain_mask 1
	spacing_to_other_terrain_types 1
}
create_terrain MED_WATER {
	base_terrain WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain DEEP_WATER {
	base_terrain MED_WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	spacing_to_other_terrain_types 1
	terrain_mask 1
}
create_terrain DLC_WATER4 {
	base_terrain DEEP_WATER
	land_percent 100
	spacing_to_other_terrain_types 2
}


/* forest clumps - might need to be tweaked */
create_terrain DLC_RAINFOREST {
	base_terrain GRASS
	land_percent 11
	number_of_clumps 16
	set_scale_by_groups
	set_avoid_player_start_areas 15
	spacing_to_other_terrain_types 1
}

/* cosmetic terrain mixing */
create_terrain DLC_JUNGLEGRASS {
	base_terrain GRASS
	land_percent 8
	number_of_clumps 8
	set_scale_by_groups
	terrain_mask 1
}
create_terrain GRASS_JUNGLE {
	base_terrain GRASS
	land_percent 8
	number_of_clumps 8
	set_scale_by_groups
	terrain_mask 1
}
create_terrain UNDERBRUSH_JUNGLE {
	base_terrain GRASS
	land_percent 8
	number_of_clumps 8
	set_scale_by_groups
	terrain_mask 1
}
create_terrain UNDERBRUSH_LEAVES {
	base_terrain GRASS
	land_percent 8
	number_of_clumps 8
	set_scale_by_groups
	terrain_mask 1
}
create_terrain LEAVES {
	base_terrain GRASS
	land_percent 8
	number_of_clumps 8
	set_scale_by_groups
	terrain_mask 1
}



/* =================================================================================================== */
<CONNECTION_GENERATION>

/* used to create the springs and streams */
create_connect_to_nonplayer_land {
	replace_terrain GRASS SHALLOW
	terrain_cost GRASS 5
	terrain_size GRASS 1 0
	
	replace_terrain DLC_RAINFOREST SHALLOW
	terrain_cost DLC_RAINFOREST 5
	terrain_size DLC_RAINFOREST 1 0
	
	replace_terrain GRAVEL_DESERT DLC_NEWSHALLOW
	terrain_cost GRAVEL_DESERT 5
	terrain_size GRAVEL_DESERT 2 0
	
	replace_terrain GRAVEL_DRY DLC_WATER5
	terrain_cost GRAVEL_DRY 5
	terrain_size GRAVEL_DRY 3 0
	
	replace_terrain GRAVEL_WET DLC_WATER5
	terrain_cost GRAVEL_WET 5
	terrain_size GRAVEL_WET 4 0
	
	replace_terrain BEACH_WET WATER
	terrain_cost BEACH_WET 5
	terrain_size BEACH_WET 5 0
	
	replace_terrain BEACH_DRY WATER
	terrain_cost BEACH_DRY 5
	terrain_size BEACH_DRY 6 0
	
	replace_terrain BEACH WATER
	terrain_cost BEACH 5
	terrain_size BEACH 6 0
	
	replace_terrain DLC_NEWSHALLOW WATER
	terrain_cost DLC_NEWSHALLOW 5
	terrain_size DLC_NEWSHALLOW 6 0
	
	replace_terrain DLC_WATER5 MED_WATER
	terrain_cost DLC_WATER5 5
	terrain_size DLC_WATER5 7 0
	
	replace_terrain WATER DEEP_WATER
	terrain_cost WATER 5
	terrain_size WATER 8 0
	
	replace_terrain MED_WATER DEEP_WATER
	terrain_cost MED_WATER 5
	terrain_size MED_WATER 9 0
	
	replace_terrain DEEP_WATER DLC_WATER4
	terrain_cost DEEP_WATER 5
	terrain_size DEEP_WATER 10 0
}

/* =================================================================================================== */
<OBJECTS_GENERATION>

/* starting objects */

create_object TOWN_CENTER {
	set_place_for_every_player
	max_distance_to_players 0
}

create_object VILLAGER {
	set_place_for_every_player
	min_distance_to_players 5
	max_distance_to_players 7
	set_circular_placement
	min_distance_group_placement 1
}

create_object SCOUT {
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

if REGICIDE
	create_object KING {
		set_place_for_every_player
		min_distance_to_players 6
		set_circular_placement
		find_closest
		min_distance_group_placement 1
	}
	create_object VILLAGER {
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 8
		set_circular_placement
	}
	create_object CASTLE {
		set_place_for_every_player
		min_distance_to_players 9
		max_distance_to_players 11
		set_circular_placement
		min_distance_group_placement 2
	}
endif

/* close fish */
create_object PLANT {
	set_place_for_every_player
	terrain_to_place_on DLC_WATER5
	find_closest
	set_gaia_object_only
	second_object DORADO
}
/* far fish */
create_object DORADO {
	number_of_groups 999
	terrain_to_place_on WATER
	max_distance_to_other_zones 4
	temp_min_distance_group_placement 15
	min_distance_group_placement 2
}
create_object SNAPPER {
	number_of_groups 999
	terrain_to_place_on WATER
	max_distance_to_other_zones 4
	temp_min_distance_group_placement 15
	min_distance_group_placement 2
}
create_object SALMON {
	number_of_groups 999
	terrain_to_place_on WATER
	max_distance_to_other_zones 4
	temp_min_distance_group_placement 15
	min_distance_group_placement 2
}
create_object DORADO {
	number_of_groups 999
	terrain_to_place_on DEEP_WATER
	max_distance_to_other_zones 6
	temp_min_distance_group_placement 6
	min_distance_group_placement 3
}
create_object MARLIN1 {
	number_of_groups 999
	terrain_to_place_on DLC_WATER4
	max_distance_to_other_zones 6
	temp_min_distance_group_placement 7
	min_distance_group_placement 4
}
create_object DORADO {
	number_of_groups 999
	terrain_to_place_on DLC_WATER4
	max_distance_to_other_zones 6
	temp_min_distance_group_placement 7
	min_distance_group_placement 4
}

create_object RELIC {
	max_distance_to_other_zones 2
	avoid_forest_zone 2
	min_distance_group_placement 1
	set_place_for_every_player
	set_gaia_object_only
	terrain_to_place_on BEACH_WET
	find_closest
}

/* forage */
create_object DLC_ORANGEBUSH {
	number_of_objects 6
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 9
	max_distance_to_players 13
	set_circular_placement
	min_distance_group_placement 4
	avoid_forest_zone 3
}

/* straggler trees */
create_object DLC_RAINTREE {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	set_circular_placement
	min_distance_group_placement 1
}
create_object DLC_RAINTREE {
	number_of_objects 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
}

/* near gold */
create_object GOLD {
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 16
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 3
}

/* medium gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 50
			min_distance_to_players 17
			max_distance_to_players 22
		percent_chance 50
			min_distance_to_players 20
			max_distance_to_players 25
	end_random
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* far gold */
create_object GOLD {
	number_of_objects 4
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 50
			min_distance_to_players 23
			max_distance_to_players 8
		percent_chance 50
			min_distance_to_players 27
			max_distance_to_players 32
	end_random
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* near stone */
create_object STONE {
	number_of_objects 5
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 13
	max_distance_to_players 18
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 3
}

/* far stone */
create_object STONE {
	number_of_objects 4
	group_placement_radius 2  
	set_tight_grouping 
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 50
			min_distance_to_players 20
			max_distance_to_players 24
		percent_chance 50
			min_distance_to_players 22
			max_distance_to_players 27
	end_random
	set_circular_placement
	min_distance_group_placement 4
	max_distance_to_other_zones 4
	avoid_forest_zone 2
}

/* sheep - one less because of the the fish */
create_object TURKEY {
	number_of_objects 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 8
	set_circular_placement
	avoid_forest_zone 2
}
create_object DLC_GOOSE {
	number_of_objects 2
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 20
			min_distance_to_players 14
			max_distance_to_players 17
		percent_chance 20
			min_distance_to_players 18
			max_distance_to_players 21
		percent_chance 20
			min_distance_to_players 20
			max_distance_to_players 23
		percent_chance 20
			min_distance_to_players 24
			max_distance_to_players 27
		percent_chance 20
			min_distance_to_players 27
			max_distance_to_players 30
	end_random
	set_circular_placement
	avoid_forest_zone 2
}

create_object DEER {
	start_random
		percent_chance 50	number_of_objects 4
		percent_chance 50	number_of_objects 3
	end_random
	number_of_groups 1
	group_placement_radius 3
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	start_random
		percent_chance 20
			min_distance_to_players 14
			max_distance_to_players	17
		percent_chance 20
			min_distance_to_players 18
			max_distance_to_players 21
		percent_chance 20
			min_distance_to_players 21
			max_distance_to_players 24
		percent_chance 20
			min_distance_to_players 24
			max_distance_to_players 27
		percent_chance 20
			min_distance_to_players 27
			max_distance_to_players 30
	end_random
	set_circular_placement
}

create_object DLC_RHINO {
	number_of_groups 2
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	min_distance_to_players 16	/* further distances removed to make boar stealing harder and reduce unlucky map generations */
	max_distance_to_players 19
	set_circular_placement
}

/* neutral gold */
create_object GOLD {
	if TINY_MAP
		number_of_groups 2
		number_of_objects 3
		temp_min_distance_group_placement 52
	elseif SMALL_MAP
		number_of_groups 3
		number_of_objects 3
		temp_min_distance_group_placement 56
	elseif MEDIUM_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 56
	elseif LARGE_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 56
	else
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 64
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	min_distance_to_players 36
	set_circular_placement
	avoid_forest_zone 2
}

/* neutral stone */
create_object STONE {
	if TINY_MAP		number_of_objects 2
	elseif SMALL_MAP	number_of_objects 2
	elseif MEDIUM_MAP
		number_of_groups 9999
		number_of_objects 3
		temp_min_distance_group_placement 56
	elseif LARGE_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 56
	elseif HUGE_MAP
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 60
	else
		number_of_groups 9999
		number_of_objects 4
		temp_min_distance_group_placement 64
	endif
    group_placement_radius 2
    set_tight_grouping
    set_gaia_object_only
	min_distance_group_placement 4
	min_distance_to_players 36
	set_circular_placement
	avoid_forest_zone 2
}

create_object DLC_TIGER {
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 33
			min_distance_to_players 30
			max_distance_to_players 33
		percent_chance 33
			min_distance_to_players 34
			max_distance_to_players 37
		percent_chance 34
			min_distance_to_players 37
			max_distance_to_players 40
	end_random
	set_circular_placement
	temp_min_distance_group_placement 15
	avoid_forest_zone 2
}

/* scattered wolves */
create_object DLC_TIGER {
	number_of_groups 9999
	set_loose_grouping
	start_random
		percent_chance 75	number_of_objects 1
		percent_chance 25	number_of_objects 2
	end_random
	set_gaia_object_only
	min_distance_group_placement 1
	temp_min_distance_group_placement 60
	avoid_forest_zone 2
	min_distance_to_players 40
	set_circular_placement
} 

/* scattered stragglers */
create_object DLC_RAINTREE {
	terrain_to_place_on GRASS
	number_of_objects 20
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players 8
	set_circular_placement
	min_distance_group_placement 1
	temp_min_distance_group_placement 12
	avoid_forest_zone 1
}

/* birds */
create_object MACAW {
	number_of_objects 6
	set_scaling_to_map_size
}



