/*
Danakil
by Zetnus and Huehuecoyotl
for the World Desert Championship


Featuring a novel tree planting mechanism


Updates:
-monkey now has proper armor and attack values
-Normal trees now cycle through slightly less total wood

This map was harvested from a mod in the official AOE2DE mod center, whose licensing agreement with Microsoft supersedes any Github restrictions: https://www.microsoft.com/en-us/servicesagreement

*/




/* =================================================================================================== */
/* constant definitions */

#const RELIC_CART 159
#const ONGRID_PLACEHOLDER 278
#const MONKEY 860
#const FLAME_D 942
#const ROCK_2 1323
#const PLANT_DEAD 1365

#const FARM_DEAD 8
#const FARM_FULL 7
#const FARM_33% 30
#const FARM_67% 31
#const MUD 76
#const WATER_GREEN 95
#const WATER_BROWN 96
#const LAVA 69

/* =================================================================================================== */
<PLAYER_SETUP>
random_placement
ai_info_map_type CUSTOM 0 0 0
behavior_version 1

/* make the monkey harmless */
effect_amount GAIA_UPGRADE_UNIT WILD_HORSE MONKEY 0
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_LINE_OF_SIGHT 2
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_SEARCH_RADIUS 2
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_ATTACK 262144
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_ATTACK 851968
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_ATTACK 1376256
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_ARMOR 196608
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_ARMOR 262144
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_SHOWN_ATTACK 0
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_SHOWN_MELEE_ARMOR 0
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_SHOWN_PIERCE_ARMOR 0
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_HITPOINTS 25
effect_amount GAIA_SET_ATTRIBUTE WILD_HORSE ATTR_NAME_ID 5853	/* smoke monkey */

/* make the jaguar faster */
effect_percent GAIA_SET_ATTRIBUTE JAGUAR ATTR_MOVE_SPEED 180

/* plantable trees */
effect_amount GAIA_UPGRADE_UNIT TURKEY RELIC_CART 0
effect_amount GAIA_SET_ATTRIBUTE TURKEY ATTR_NAME_ID 5718 /* 12418 */
effect_amount GAIA_SET_ATTRIBUTE TURKEY ATTR_ICON_ID 32 
effect_amount GAIA_SET_ATTRIBUTE TURKEY ATTR_HITPOINTS 12
effect_amount SET_ATTRIBUTE TURKEY ATTR_DEAD_ID 1348 /* Italian pine tree */
effect_amount GAIA_SET_ATTRIBUTE TURKEY ATTR_MOVE_SPEED 0

effect_amount GAIA_SET_ATTRIBUTE ITALIAN_PINETREE ATTR_STORAGE_VALUE 300
effect_amount SET_ATTRIBUTE ITALIAN_PINETREE ATTR_DEAD_ID 59 /* forage */
effect_amount GAIA_UPGRADE_UNIT FORAGE DLC_BAOBABTREE 0 
effect_amount GAIA_SET_ATTRIBUTE FORAGE ATTR_HITPOINTS 9999
effect_amount GAIA_SET_ATTRIBUTE FORAGE ATTR_STORAGE_VALUE 0
/* upgrade forage into unchoppable tree */

/* other trees - these end as stumps */
effect_amount SET_ATTRIBUTE DLC_ACACIATREE ATTR_DEAD_ID 1053 /* bush B */
effect_amount SET_ATTRIBUTE DLC_DRAGONTREE ATTR_DEAD_ID 1053 /* bush B */
effect_amount SET_ATTRIBUTE OLIVE_TREE ATTR_DEAD_ID 1053 /* bush B */
effect_amount SET_ATTRIBUTE DLC_AFRICANBUSH ATTR_DEAD_ID 348 /* bamboo */
effect_amount GAIA_SET_ATTRIBUTE BAMBOO_TREE ATTR_STORAGE_VALUE 50
effect_amount SET_ATTRIBUTE BAMBOO_TREE ATTR_DEAD_ID 1350 /* reedtree */

effect_amount GAIA_SET_ATTRIBUTE DLC_BOULDER_A ATTR_OCCLUSION_MODE 2
effect_amount GAIA_SET_ATTRIBUTE DLC_BOULDER_B ATTR_OCCLUSION_MODE 2
effect_amount GAIA_SET_ATTRIBUTE DLC_BOULDER_C ATTR_OCCLUSION_MODE 2

/* Disabling Trade Carts and Cogs for Multi-team Games */
if 2_TEAM_GAME
else
  #const TRADE_CART_ENABLER 161
  #const TRADE_COG_ENABLER 180
  #const TRADE_CARAVAN_ENABLER 48
  #const HINDU_CARAVANSERAI_ENABLER 518
  #const PERSIAN_CARAVANSERAI_ENABLER 552  
  #const SILK_ROAD 499
  effect_amount DISABLE_TECH TRADE_COG_ENABLER ATTR_DISABLE 180
  effect_amount DISABLE_TECH TRADE_CART_ENABLER ATTR_DISABLE 161
  effect_amount DISABLE_TECH TRADE_CARAVAN_ENABLER ATTR_DISABLE 48
  effect_amount DISABLE_TECH HINDU_CARAVANSERAI_ENABLER ATTR_DISABLE 518
  effect_amount DISABLE_TECH PERSIAN_CARAVANSERAI_ENABLER ATTR_DISABLE 552
  effect_amount DISABLE_TECH SILK_ROAD ATTR_DISABLE 499    
endif

/* =================================================================================================== */
<LAND_GENERATION> 

base_terrain DLC_CRACKED

create_player_lands {
	terrain_type DIRT
	land_percent 50
	right_border 13 left_border 13 top_border 13 bottom_border 13
	border_fuzziness 10
	base_size 15
	zone 1
	base_elevation 3
	circle_radius 28 4
	other_zone_avoidance_distance 5
}

/* the center */
create_land {
	terrain_type DLC_DRYGRASS
	land_percent 15
	right_border 35 left_border 35 top_border 35 bottom_border 35
	base_size 14
	zone 1
	land_id 1
	land_position 50 50
	base_elevation 4
}

/* the gold and stone pits */
start_random
	percent_chance 50 #define GOLD_IN_CORNERS
	percent_chance 50 #define STONE_IN_CORNERS
end_random

create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 2
	land_id 2
	land_position 1 1  /* left corner */
	left_border -15
	right_border 90
	top_border -15
	bottom_border 90
	other_zone_avoidance_distance 5
}

create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 2
	land_id 2
	land_position 99 99  /* right corner */
	left_border 90
	right_border -15
	top_border 90
	bottom_border -15
	other_zone_avoidance_distance 5
}
create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 2
	land_id 2
	land_position 99 1  /* top */
	left_border 90
	right_border -15
	top_border -15
	bottom_border 90
	other_zone_avoidance_distance 5
}
create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 2
	land_id 2
	land_position 1 99  /* bottom */
	left_border -15
	right_border 90
	top_border 90
	bottom_border -15
	other_zone_avoidance_distance 5
}

/* sides */
create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 3
	land_id 3
	land_position 50 2  /* north west */
	left_border 40
	right_border 40
	top_border -15
	bottom_border 90
	other_zone_avoidance_distance 5
}
create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 3
	land_id 3
	land_position 98 50  /* north east */
	left_border 90
	right_border -15
	top_border 40
	bottom_border 40
	other_zone_avoidance_distance 5
}
create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 3
	land_id 3
	land_position 50 98  /* south east */
	left_border 40
	right_border 40
	top_border 90
	bottom_border -15
	other_zone_avoidance_distance 5
}
create_land {
	terrain_type DLC_DESERTGRAVEL
	number_of_tiles 250
	base_size 4
	zone 3
	land_id 3
	land_position 2 55  /* south west */
	left_border -15
	right_border 90
	top_border 40
	bottom_border 40
	other_zone_avoidance_distance 5
}

/* =================================================================================================== */
<ELEVATION_GENERATION> 

/* the center */
create_elevation 9 { base_terrain DLC_DRYGRASS number_of_tiles 999999 enable_balanced_elevation spacing 4 }
create_elevation 9 { base_terrain DLC_DRYGRASS number_of_tiles 999999 enable_balanced_elevation spacing 4 }
create_elevation 9 { base_terrain DLC_DRYGRASS number_of_tiles 999999 enable_balanced_elevation spacing 4 }
create_elevation 9 { base_terrain DLC_DRYGRASS number_of_tiles 999999 enable_balanced_elevation spacing 4 }
create_elevation 9 { base_terrain DLC_DRYGRASS number_of_tiles 999999 enable_balanced_elevation spacing 4 }

/* the outside */
create_elevation 1 {
	base_terrain DLC_CRACKED
	number_of_clumps 320
	if TINY_MAP number_of_tiles 350
	elseif SMALL_MAP number_of_tiles 525
	elseif MEDIUM_MAP number_of_tiles 700
	elseif LARGE_MAP number_of_tiles 1000
	elseif HUGE_MAP number_of_tiles 1200
	elseif GIGANTIC_MAP number_of_tiles 1450
	elseif LUDIKRIS_MAP number_of_tiles 5750
	endif
	set_scale_by_groups
	enable_balanced_elevation
	}

/* =================================================================================================== */
<TERRAIN_GENERATION>

color_correction CC_DESERT

/* farm masking */
create_terrain FARM_33% {
	base_terrain DIRT
	land_percent 8
	number_of_clumps 8
	terrain_mask 2
	set_scale_by_groups
}
create_terrain FARM_67% {
	base_terrain DIRT
	land_percent 8
	number_of_clumps 8
	terrain_mask 2
	set_scale_by_groups
}
create_terrain FARM_FULL {
	base_terrain DIRT
	land_percent 8
	number_of_clumps 8
	terrain_mask 2
	set_scale_by_groups
}

create_terrain FARM_DEAD {
	base_terrain DIRT
	land_percent 100
	number_of_clumps 9320
	terrain_mask 2
	spacing_to_other_terrain_types 1
}

/* center */
create_terrain MUD {
	base_terrain DLC_DRYGRASS
	land_percent 100
	number_of_clumps 9320
	spacing_to_other_terrain_types 2
}
create_terrain GRASS {
	base_terrain MUD
	land_percent 100
	number_of_clumps 9320
	terrain_mask 2
	spacing_to_other_terrain_types 1
}

/* pits */
create_terrain LAVA {
	base_terrain DLC_DESERTGRAVEL
	land_percent 100
	number_of_clumps 9320
	terrain_mask 1
}

/* outside */
create_terrain FARM_DEAD {
	base_terrain DLC_CRACKED
	land_percent 100
	number_of_clumps 9320
	terrain_mask 1
	height_limits 2 3
}
create_terrain WATER_GREEN {
	base_terrain DLC_CRACKED
	land_percent 1
	number_of_clumps 10
	set_scale_by_groups
	spacing_to_other_terrain_types 1
	set_flat_terrain_only
	height_limits 0 0
}
create_terrain DLC_NEWSHALLOW {
	base_terrain WATER_GREEN
	land_percent 1
	number_of_clumps 100
	terrain_mask 1
}

create_terrain DLC_NEWSHALLOW {
	base_terrain DLC_CRACKED
	land_percent 1
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
	height_limits 0 0
}
create_terrain SHALLOW {
	base_terrain DLC_CRACKED
	land_percent 1
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
	height_limits 0 10
}
create_terrain WATER_GREEN {
	base_terrain DLC_CRACKED
	land_percent 1
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
	height_limits 0 0
}
create_terrain WATER_BROWN {
	base_terrain DLC_CRACKED
	land_percent 1
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
	height_limits 0 0
}

create_terrain DLC_BEACH4 {
	base_terrain DLC_CRACKED
	land_percent 10
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
}
create_terrain DESERT {
	base_terrain DLC_CRACKED
	land_percent 10
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
}
create_terrain DLC_QUICKSAND {
	base_terrain DLC_CRACKED
	land_percent 10
	number_of_clumps 40
	set_scale_by_groups
	terrain_mask 1
	clumping_factor -25
}


/* =================================================================================================== */
<CONNECTION_GENERATION>

/* just for the fence openings */
create_connect_to_nonplayer_land {
replace_terrain MUD DLC_DRYGRASS

terrain_size MUD 2
terrain_size DLC_DRYGRASS 0
terrain_size GRASS 0
}

/* =================================================================================================== */
<OBJECTS_GENERATION>

/* blank objects to help distribute resources in a more circular manner */
create_object BLANKOBJECT {
	number_of_objects 320
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 9
	actor_area 6009
	actor_area_radius 0
}
create_object BLANKOBJECT {
	number_of_objects 370
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 11
	actor_area 6011
	actor_area_radius 0
}
create_object BLANKOBJECT {
	number_of_objects 440
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 13
	actor_area 6013
	actor_area_radius 0
}
create_object BLANKOBJECT {
	number_of_objects 465
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 15
	actor_area 6015
	actor_area_radius 0
}
create_object BLANKOBJECT {
	number_of_objects 500
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 17
	actor_area 6017
	actor_area_radius 0
}
create_object BLANKOBJECT  {
	number_of_objects rnd(500,600)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 20
	actor_area 6020
	actor_area_radius 0
}
create_object BLANKOBJECT {
	number_of_objects rnd(600,700)
	set_gaia_object_only
	set_place_for_every_player
	find_closest
	min_distance_to_players 23
	actor_area 6023
	actor_area_radius 0
}


/* the center */

/* big hut */
create_object DLC_AFRICANHUT_C {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	max_distance_to_players 0
	second_object PATH_3
	make_indestructible
}

/* small huts */
create_object DLC_AFRICANHUT_A {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	terrain_to_place_on DLC_DRYGRASS
	make_indestructible
}
create_object DLC_AFRICANHUT_B {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	terrain_to_place_on DLC_DRYGRASS
	make_indestructible
}
create_object DLC_AFRICANHUT_D {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	terrain_to_place_on DLC_DRYGRASS
	make_indestructible
}
create_object DLC_AFRICANHUT_E {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	terrain_to_place_on DLC_DRYGRASS
	make_indestructible
}
create_object DLC_AFRICANHUT_F {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	terrain_to_place_on DLC_DRYGRASS
	make_indestructible
}
create_object DLC_AFRICANHUT_G {
	number_of_objects 1
	place_on_specific_land_id 1
	set_gaia_object_only
	set_gaia_unconvertible
	terrain_to_place_on DLC_DRYGRASS
	make_indestructible
}

create_object DLC_FENCE {
	number_of_objects 9320
	terrain_to_place_on MUD
	set_gaia_object_only
	set_gaia_unconvertible
}

/* center trees */
create_object DLC_ACACIATREE {
	number_of_objects 13
	terrain_to_place_on GRASS
	set_scaling_to_map_size
}
create_object DLC_DRAGONTREE {
	number_of_objects 13
	terrain_to_place_on GRASS
	set_scaling_to_map_size
}

/* plantable relic carts */
create_object TURKEY {
	number_of_objects 30
	terrain_to_place_on GRASS
	set_scaling_to_map_size
	temp_min_distance_group_placement 1
}

/* harmless monkeys */
create_object WILD_HORSE {
	number_of_objects 5
	group_variance 2
	group_placement_radius 3
	terrain_to_place_on GRASS
	set_scaling_to_map_size
	temp_min_distance_group_placement 6
}

/* usless plants as decoration */

create_object PLANT_DEAD {
	number_of_objects 9320
	terrain_to_place_on FARM_33%
	set_scaling_to_map_size
	temp_min_distance_group_placement 3
}
create_object PLANT_DEAD {
	number_of_objects 9320
	terrain_to_place_on FARM_67%
	set_scaling_to_map_size
	temp_min_distance_group_placement 3
}
create_object PLANT_DEAD {
	number_of_objects 9320
	terrain_to_place_on FARM_FULL
	set_scaling_to_map_size
	temp_min_distance_group_placement 3
}

/* farmland trees */
create_object DLC_ACACIATREE {
	number_of_objects 20
	terrain_to_place_on DIRT
	set_scaling_to_map_size
	avoid_forest_zone 1
	temp_min_distance_group_placement 5
	min_distance_to_players 12
	set_circular_placement
}
create_object OLIVE_TREE {
	number_of_objects 20
	terrain_to_place_on DIRT
	set_scaling_to_map_size
	avoid_forest_zone 1
	temp_min_distance_group_placement 5
	min_distance_to_players 12
	set_circular_placement
}
create_object DLC_DRAGONTREE {
	number_of_objects 20
	terrain_to_place_on DIRT
	set_scaling_to_map_size
	avoid_forest_zone 1
	temp_min_distance_group_placement 5
	min_distance_to_players 12
	set_circular_placement
}
create_object DLC_AFRICANBUSH {
	number_of_objects 9320
	terrain_to_place_on DIRT
	set_scaling_to_map_size
	avoid_forest_zone 1
	temp_min_distance_group_placement 1
	min_distance_to_players 10
	set_circular_placement
}

/* player starting units */
create_object TOWN_CENTER {
	set_place_for_every_player
	max_distance_to_players 0
}
create_object FARM {
	set_place_for_every_player
	number_of_objects 3
	find_closest
	second_object VILLAGER
}
create_object FARM {
	set_place_for_every_player
	find_closest
	if REGICIDE second_object KING
	else second_object SCOUT
	endif
}

/* near forage */
create_object DLC_ORANGEBUSH {
	number_of_objects 6
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 9
	avoid_actor_area 6009
	find_closest
	layer_to_place_on DIRT
	min_distance_group_placement 4
	avoid_forest_zone 2
	actor_area 14
	actor_area_radius 10
	ignore_terrain_restrictions
}

/* near gold */
create_object GOLD {
	number_of_objects 2
	number_of_groups 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 13
	avoid_actor_area 6013
	find_closest
	layer_to_place_on DIRT
	min_distance_group_placement 4
	temp_min_distance_group_placement 21
	avoid_forest_zone 2
	ignore_terrain_restrictions
}

/* near gold */
create_object STONE {
	number_of_objects 2
	number_of_groups 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 13
	avoid_actor_area 6013
	find_closest
	layer_to_place_on DIRT
	min_distance_group_placement 4
	temp_min_distance_group_placement 30
	avoid_forest_zone 2
	ignore_terrain_restrictions
}

create_object DLC_GOAT {
	number_of_objects 4
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 7
	set_circular_placement
	find_closest
}
create_object DLC_GOAT {
	number_of_objects 2
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 20
			min_distance_to_players 13
			avoid_actor_area 6013
			find_closest
		percent_chance 20
			min_distance_to_players 15
			avoid_actor_area 6015
			find_closest
		percent_chance 20
			min_distance_to_players 17
			avoid_actor_area 6017
			find_closest
		percent_chance 20
			min_distance_to_players 20
			avoid_actor_area 6020
			find_closest
		percent_chance 20
			min_distance_to_players 23
			avoid_actor_area 6023
			find_closest
	end_random
	min_distance_group_placement 2
	avoid_forest_zone 2
	layer_to_place_on DIRT
}
create_object DLC_GOAT {
	number_of_objects 2
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	start_random
		percent_chance 20
			min_distance_to_players 13
			avoid_actor_area 6013
			find_closest
		percent_chance 20
			min_distance_to_players 15
			avoid_actor_area 6015
			find_closest
		percent_chance 20
			min_distance_to_players 17
			avoid_actor_area 6017
			find_closest
		percent_chance 20
			min_distance_to_players 20
			avoid_actor_area 6020
			find_closest
		percent_chance 20
			min_distance_to_players 23
			avoid_actor_area 6023
			find_closest
	end_random
	min_distance_group_placement 2
	avoid_forest_zone 2
	layer_to_place_on DIRT
}

create_object GAZELLE {
	start_random
		percent_chance 50	number_of_objects 4
		percent_chance 50	number_of_objects 3
	end_random
	number_of_groups 1
	group_placement_radius 3
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	avoid_forest_zone 2
	min_distance_group_placement 2
	start_random
		percent_chance 25
			min_distance_to_players 15
			avoid_actor_area 6015
			find_closest
		percent_chance 25
			min_distance_to_players 17
			avoid_actor_area 6017
			find_closest
		percent_chance 25
			min_distance_to_players 20
			avoid_actor_area 6020
			find_closest
		percent_chance 25
			min_distance_to_players 23
			avoid_actor_area 6023
			find_closest
	end_random
	layer_to_place_on DIRT
	avoid_actor_area 14
}


/* scattered plantable trees */
create_object TURKEY {
	number_of_objects 10
	number_of_groups 2
	set_scaling_to_player_number
	set_loose_grouping
	group_placement_radius 4
	set_gaia_object_only
	min_distance_to_players 25
	temp_min_distance_group_placement 30
	min_distance_group_placement 2
	avoid_forest_zone 1
	layer_to_place_on DIRT
}

/* gold and stone pits */
create_object FLAME_D {
	number_of_objects 22
	place_on_specific_land_id 2
	find_closest
	second_object SEA_ROCKS_1
}
create_object FLAME_D {
	number_of_objects 22
	place_on_specific_land_id 2
	find_closest
	second_object SEA_ROCKS_1
}
create_object FLAME_D {
	number_of_objects 22
	place_on_specific_land_id 3
	find_closest
	second_object SEA_ROCKS_1
}
create_object FLAME_D {
	number_of_objects 22
	place_on_specific_land_id 3
	find_closest
	second_object SEA_ROCKS_1
}

create_object SEA_ROCKS_2 {
	number_of_objects 1
	place_on_specific_land_id 2
	find_closest
}
create_object SEA_ROCKS_2 {
	number_of_objects 11
	place_on_specific_land_id 2
	find_closest
}
create_object SEA_ROCKS_2 {
	number_of_objects 1
	place_on_specific_land_id 3
	find_closest
}
create_object SEA_ROCKS_2 {
	number_of_objects 1
	place_on_specific_land_id 3
	find_closest
}

/* create_object ROCK_2 {
	number_of_objects 2
	place_on_specific_land_id 2
	find_closest
}
create_object ROCK_2 {
	number_of_objects 2
	place_on_specific_land_id 2
	find_closest
}
create_object ROCK_2 {
	number_of_objects 2
	place_on_specific_land_id 3
	find_closest
}
create_object ROCK_2 {
	number_of_objects 2
	place_on_specific_land_id 3
	find_closest
}
*/

create_object CRACKS {
	number_of_groups 40
	number_of_objects 1
	terrain_to_place_on DLC_DESERTGRAVEL
	temp_min_distance_group_placement 3
}

create_object SEA_ROCKS_1 {
	number_of_groups 40
	number_of_objects 1
	terrain_to_place_on DLC_DESERTGRAVEL
	temp_min_distance_group_placement 3
}


create_object GOLD {
    number_of_groups 2
	number_of_objects 3
	set_tight_grouping
	set_scaling_to_player_number 
    layer_to_place_on LAVA
	if GOLD_IN_CORNERS place_on_specific_land_id 2
	else place_on_specific_land_id 3
	endif
	min_distance_to_players 5
	max_distance_to_players 15
	set_circular_placement
    set_scaling_to_map_size
}
create_object STONE {
    number_of_groups 1
	number_of_objects 3
	set_tight_grouping
	set_scaling_to_player_number 
    layer_to_place_on LAVA
	if STONE_IN_CORNERS place_on_specific_land_id 2
	else place_on_specific_land_id 3
	endif
	min_distance_to_players 5
	max_distance_to_players 15
	set_circular_placement
    set_scaling_to_map_size
}


/* outside */
create_object DLC_BOULDER_A {
	number_of_objects 25
	terrain_to_place_on DLC_CRACKED
	set_scaling_to_map_size
	actor_area 3333
	actor_area_radius 2
}
create_object DLC_BOULDER_B {
	number_of_objects 15
	terrain_to_place_on DLC_CRACKED
	set_scaling_to_map_size
	actor_area 3333
	actor_area_radius 2
}
create_object DLC_BOULDER_C {
	number_of_objects 10
	terrain_to_place_on BEACH
	set_scaling_to_map_size
	temp_min_distance_group_placement 3
	actor_area 3333
	actor_area_radius 2
}
create_object DLC_BOULDER_B {
	number_of_objects 5
	terrain_to_place_on BEACH
	set_scaling_to_map_size
	temp_min_distance_group_placement 2
	actor_area 3333
	actor_area_radius 2
}
create_object ROCK_2 {
	number_of_objects 30
	terrain_to_place_on BEACH
	set_scaling_to_map_size
	temp_min_distance_group_placement 1
	actor_area 3333
	actor_area_radius 2
}
create_object DLC_BOULDER_A {
	number_of_objects 9320
	terrain_to_place_on BEACH
	set_scaling_to_map_size
	actor_area 3333
	actor_area_radius 2
}


create_object RELIC
{
	if TINY_MAP
		number_of_objects 5
		temp_min_distance_group_placement 35
	elseif SMALL_MAP
		number_of_objects 5
		temp_min_distance_group_placement 38
	elseif MEDIUM_MAP
		number_of_objects 5
		temp_min_distance_group_placement 38
	elseif LARGE_MAP
		number_of_objects 7
		temp_min_distance_group_placement 48
	elseif HUGE_MAP
		number_of_objects 8
		temp_min_distance_group_placement 52
	else
		number_of_objects 999
		temp_min_distance_group_placement 52 
	endif
	min_distance_to_players 25
	set_circular_placement
	avoid_forest_zone 2
	min_distance_group_placement 1
	avoid_actor_area 3333
	terrain_to_place_on DLC_CRACKED
}

create_object WOLF {
	number_of_groups 2
	set_gaia_object_only
	set_scaling_to_player_number
	min_distance_to_players 25
	set_circular_placement
	temp_min_distance_group_placement 18
	terrain_to_place_on DLC_CRACKED
	min_distance_group_placement 1
	actor_area 67
	actor_area_radius 18
}

create_object JAGUAR {
	number_of_groups 2
	set_gaia_object_only
	set_scaling_to_player_number
	min_distance_to_players 25
	set_circular_placement
	temp_min_distance_group_placement 18
	terrain_to_place_on DLC_CRACKED
	min_distance_group_placement 1
	avoid_actor_area 67
}

create_object DLC_IBEX {
	number_of_groups 3
	set_gaia_object_only
	set_scaling_to_player_number
	min_distance_to_players 25
	set_circular_placement
	temp_min_distance_group_placement 15
	terrain_to_place_on DLC_CRACKED
	min_distance_group_placement 2
}


create_object DLC_VULTURE {
	number_of_objects 4
	set_scaling_to_map_size
}



